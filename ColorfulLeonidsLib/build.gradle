apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionName = '1.3.3'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
    }

    buildTypes {
    }
}

def siteUrl = 'http://github.com/jasonlamkk/ColorfulLeonids/'
def gitUrl = 'https://github.com/jasonlamkk/ColorfulLeonids.git'
group = "com.github.jasonlamkk"
version = versionName


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "ColorfulLeonids"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        dryRun = true
        publish = false
        version {
            name = versionName //Bintray logical version name
//            released = //Optional - Date of the version release. 2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            vcsTag = versionName
        }
    }
}


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                artifactId 'ColorfulLeonidsLib'
                version = versionName
                // Add your description here
                name 'ColorfulLeonids'
                description = 'A Particle System Library for standard Android UI.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'jasonlamkk'
                        name 'Jason Lam'
                        email 'jasonlamkk@users.noreply.github.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task classesJar(type: Jar) {
    //from android.sourceSets.main.java
    from 'libs/'
    archiveName "ColorfulLeonidsLib-${versionName}.jar"
}
task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    ext.cp = android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    }
    classpath = files(ext.cp)
}

artifacts {
    archives sourcesJar
    archives classesJar
}